plugins {
	id 'eclipse'
	id 'idea'
	id 'net.minecraftforge.gradle' version '[6.0,6.2)'
	id 'org.spongepowered.mixin' version '0.7.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

version = 'v1.9.11'
group = 'net.geforcemods.securitycraft'
archivesBaseName = 'securitycraft'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
	duplicatesStrategy = 'include'

	//replace ${version} in mods.toml
	inputs.property 'version', project.version

	from(sourceSets.main.resources.srcDirs) {
		include 'META-INF/mods.toml'

		expand 'version': project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'META-INF/mods.toml'
	}

    //minify json resources
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

sourceSets {
	main {
		resources {
			srcDirs += "src/generated/resources" //include generated files
		}
	}
}

minecraft {
    mappings channel: 'parchment', version: '2022.03.06-1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	
    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=securitycraft.mixins.json'

            mods {
                securitycraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            args '-nogui', '-mixin.config=securitycraft.mixins.json'

            mods {
                securitycraft {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'securitycraft', '--all', '--output', file('src/generated/resources/')

            mods {
                securitycraft {
                    source sourceSets.main
                }
            }
        }
	}
}

repositories { 
    maven {
    	url "https://cursemaven.com/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'
    annotationProcessor 'org.spongepowered:mixin:0.8.3:processor'

    implementation fg.deobf("curse.maven:architectury-api-419699:3857643") //ftb teams dependency
    implementation fg.deobf("curse.maven:ftb-library-forge-404465:3553840") //ftb teams dependency
    implementation fg.deobf("curse.maven:ftb-teams-forge-404468:3535953")
    implementation fg.deobf("curse.maven:jei-238222:4060770")
    implementation fg.deobf("curse.maven:the-one-probe-245211:3752096")
    implementation fg.deobf("curse.maven:jade-324717:3910873")
    implementation fg.deobf("curse.maven:betterf3-401648:4030536")
    implementation fg.deobf("curse.maven:cloth-config-forge-348521:3972422") //betterf3 dependency
	compileOnly fg.deobf("curse.maven:lycanites-mobs-224770:3516607") //compileOnly still adds the dep to runtime for some reason, and this mod does not work in dev. to fix this, remove the mods.toml file from this gradle dependency's jar file so the mod does not get loaded ingame
    implementation fg.deobf("curse.maven:projecte-226410:3736621")
	implementation fg.deobf("curse.maven:embeddium-908741:5067969")
}

mixin {
    add sourceSets.main, "securitycraft.refmap.json"
}

jar {
	exclude('net/geforcemods/securitycraft/datagen/**') //exclude files from the built jar that are only used to generate the assets & data

    manifest {
        attributes(["Specification-Title": "SecurityCraft",
                    "Specification-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "SecurityCraft",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep",
                    "MixinConfigs": "securitycraft.mixins.json"],)
    }
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}