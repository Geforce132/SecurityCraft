buildscript {
	repositories {
		jcenter()
		maven { url = "http://maven.minecraftforge.net" }
		maven { url = 'https://repo.spongepowered.org/maven' }
		maven { url = 'https://plugins.gradle.org/m2' }
	}

	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
	}
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = 'v1.9.10'
group = 'net.geforcemods.securitycraft'
archivesBaseName = 'securitycraft'

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "1.12.2-14.23.5.2826"
	runDir = "run"
	clientRunArgs = ["--username Dev"]
	serverRunArgs = ["nogui"]
	coreMod = 'net.geforcemods.securitycraft.SecurityCraftLoadingPlugin'
	mappings = "stable_39"
}

repositories {
	maven { //main jei maven
		name = "Progwml6 maven"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven { //jei fallback
		name = "ModMaven"
		url = "modmaven.k-4u.nl"
	}
	maven { // TOP
		name 'tterrag maven'
		url "http://maven.tterrag.com/"
	}
	maven {
		name = "CurseMaven"
		url = 'https://cursemaven.com/'
	}
	maven {
		name = "Sponge Maven"
		url = 'https://repo.spongepowered.org/maven'
	}
}

dependencies {
	annotationProcessor 'org.spongepowered:mixin:0.8.3:processor'
	compile 'org.spongepowered:mixin:0.8.3'
	deobfProvided "curse.maven:hwyla-253449:2568751"
	deobfProvided "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17"
	deobfProvided "curse.maven:ftb-library-legacy-forge-237167:2985811"
	deobfProvided "curse.maven:ftb-utilities-forge-237102:3157548"
	deobfProvided "curse.maven:cyclic-239286:2883487"
	deobfProvided "curse.maven:icbm-classic-244451:4399322"
	deobfProvided "curse.maven:lycanites-mobs-224770:2935745"
	deobfProvided "curse.maven:projecte-226410:2702991"
	deobfProvided 'mezz.jei:jei_1.12.2:4.16.1.302'
}

mixin {
	add sourceSets.main, 'securitycraft.refmap.json'
}

shadowJar {
	dependencies {
		include(dependency('org.spongepowered:mixin:0.8.3'))
	}

	exclude 'dummyThing'
	classifier = ''
}

task noMixinJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	exclude 'dummyThing'
	classifier = 'no_mixin'
	relocators = shadowJar.relocators
	
	manifest {
		inheritFrom jar.manifest
	}

	from sourceSets.main.output
}

build.dependsOn(shadowJar)

reobf {
	shadowJar {
		mappingType = 'SEARGE'
		classpath = sourceSets.main.compileClasspath
	}
	noMixinJar {}
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	
	//minify json resources
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
			File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
		}
	}
}

jar.manifest.attributes(
	'FMLCorePluginContainsFMLMod': true,
	'FMLCorePlugin': 'net.geforcemods.securitycraft.SecurityCraftLoadingPlugin',
	'ForceLoadAsMod': true,
	'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
	'FMLAT': 'securitycraft_at.cfg'
)
