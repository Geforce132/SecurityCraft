plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.moddev' version '2.0.76'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

version = '1.9.12'
group = 'net.geforcemods.securitycraft'

base {
	archivesName = 'securitycraft'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
	duplicatesStrategy = 'include'

	//minify json resources
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
			File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources' //include generated files
}

neoForge {
	version = "20.4.239"
	//validateAccessTransformers = true //Can't use, since NeoForge itself ships invalid ATs

	runs {
		configureEach {
			logLevel = org.slf4j.event.Level.DEBUG
			ideName = "SC ${project.mc_version} " + ideName.get()
		}

		client {
			client()
			gameDirectory = project.file('run/client')
		}

		client2 {
			client()
			gameDirectory = project.file('run/client')
			programArguments.addAll('--username', 'Dev2')
		}

		server {
			server()
			gameDirectory = project.file('run/server')
			programArgument('-nogui')
		}

		data {
			data()
			gameDirectory = project.file('run/data')
			programArguments.addAll('--mod', 'securitycraft', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath())
		}
	}

	mods {
		securitycraft {
			sourceSet(sourceSets.main)
		}
	}
}

repositories {
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	implementation "curse.maven:architectury-api-419699:5173990" //ftb teams dependency
	implementation "curse.maven:ftb-library-forge-404465:5205161" //ftb teams dependency
	implementation "curse.maven:ftb-teams-forge-404468:5202951"
	implementation "curse.maven:jei-238222:5044537"
	implementation "curse.maven:the-one-probe-245211:5159289"
	implementation "curse.maven:jade-324717:5109393"
	implementation "curse.maven:betterf3-401648:5199616"
	implementation "curse.maven:cloth-config-348521:4973457" //betterf3 dependency
	implementation "curse.maven:wthit-forge-455982:5208362"
	implementation "curse.maven:badpackets-615134:5130415" //wthit dependency
	implementation "curse.maven:embeddium-908741:5077203"
}

tasks.named('jar', Jar).configure {
	exclude('net/geforcemods/securitycraft/datagen/**') //exclude files from the built jar that are only used to generate the assets & data
	archiveFileName = "[${project.mc_version}] SecurityCraft v${version}.jar"
	manifest {
		attributes(["Specification-Title": "SecurityCraft",
					"Specification-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep",
					"Specification-Version": "${version}",
					"Implementation-Title": "SecurityCraft",
					"Implementation-Version": "${version}",
					"Implementation-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep"],)
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release = 17
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}