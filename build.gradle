plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.moddev' version '2.0.89'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

version = '1.10-beta3'
group = 'net.geforcemods.securitycraft'

base {
	archivesName = 'securitycraft'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
	duplicatesStrategy = 'include'

	//minify json resources
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
			File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources' //include generated files
}

neoForge {
	version = "21.5.75"
	validateAccessTransformers = true

	runs {
		configureEach {
			logLevel = org.slf4j.event.Level.DEBUG
			gameDirectory = project.file('run/' + name)
			ideName = "SC ${project.mc_version} " + ideName.get()
		}

		client {
			client()
		}

		client2 {
			client()
			gameDirectory = project.file('run/client')
			programArguments.addAll('--username', 'Dev2')
		}

		server {
			server()
			programArgument('-nogui')
		}

		data {
			clientData()
			programArguments.addAll('--mod', 'securitycraft', '--all', '--output', file('src/generated/resources/').getAbsolutePath())
		}
	}

	mods {
		securitycraft {
			sourceSet(sourceSets.main)
		}
	}
}

repositories {
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}

	exclusiveContent {
		forRepository {
			maven {
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
//	implementation "curse.maven:architectury-api-419699:5553800" //ftb teams dependency
//	implementation "curse.maven:ftb-library-forge-404465:5557408" //ftb teams dependency
//	implementation "curse.maven:ftb-teams-forge-404468:5448371"
	implementation "curse.maven:jei-238222:6617262"
//	implementation "curse.maven:the-one-probe-245211:5502323"
	implementation "curse.maven:jade-324717:6524708"
	implementation "curse.maven:betterf3-401648:6364907"
	implementation "curse.maven:cloth-config-348521:6351757" //betterf3 dependency
	implementation "curse.maven:wthit-forge-455982:6613525"
	implementation "curse.maven:badpackets-615134:6067146" //wthit dependency
//	implementation "curse.maven:projecte-226410:3955047"
//	implementation "curse.maven:embeddium-908741:6116910"
	implementation "maven.modrinth:sodium:mc1.21.5-0.6.13-neoforge" //incompatible with embeddium
}

tasks.named('jar', Jar).configure {
	exclude('net/geforcemods/securitycraft/datagen/**') //exclude files from the built jar that are only used to generate the assets & data
	archiveFileName = "[${project.mc_version}] SecurityCraft v${version}.jar"
	manifest {
		attributes(["Specification-Title": "SecurityCraft",
					"Specification-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep",
					"Specification-Version": "${version}",
					"Implementation-Title": "SecurityCraft",
					"Implementation-Version": "${version}",
					"Implementation-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep"],)
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release = 21
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}