buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
	id "com.wynprice.cursemaven" version "2.1.1"
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = '1.0'
group = 'net.geforcemods.securitycraft'
archivesBaseName = 'securitycraft'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

processResources { 
	exclude ".cache"
	
    //minify json resources
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

sourceSets {
	main {
		resources {
			srcDirs += "src/generated/resources" //include generated files
		}
	}
}

minecraft {
    mappings channel: 'snapshot', version: '20200723-1.16.1'
	
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                securitycraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                securitycraft {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'securitycraft', '--all', '--output', file('src/generated/resources/')

            mods {
                securitycraft {
                    source sourceSets.main
                }
            }
        }
	}
}

repositories {  
    maven { // HWYLA
		url "http://maven.tehnut.info"
	}
	maven { // TOP
		name 'tterrag maven'
		url "http://maven.tterrag.com/"
	}
    maven { // JEI
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven { // JEI fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.3-34.0.3'
    
	implementation fg.deobf("mezz.jei:jei-1.16.2:7.3.2.25")
	implementation fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.3-beta-6")
    implementation fg.deobf("mcp.mobius.waila:Hwyla:1.10.11-B78_1.16.2")
    compileOnly fg.deobf("curse.maven:cyclic:3037395")
}

jar {
	exclude('net/geforcemods/securitycraft/datagen/**') //exclude files from the built jar that are only used to generate the assets

    manifest {
        attributes(["Specification-Title": "SecurityCraft",
                    "Specification-Vendor": "Geforce, bl4ckscor3",
                    "Specification-Version": "v1.8.19.3",
                    "Implementation-Title": "SecurityCraft",
                    "Implementation-Version": "v1.8.19.3",
                    "Implementation-Vendor" :"Geforce, bl4ckscor3",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}