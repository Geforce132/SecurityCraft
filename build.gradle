plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.moddev' version '2.0.99'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

version = '1.10-beta1'
group = 'net.geforcemods.securitycraft'

base {
	archivesName = 'securitycraft'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
	duplicatesStrategy = 'include'

	//minify json resources
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
			File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources' //include generated files
}

neoForge {
	version = "21.8.13"
	validateAccessTransformers = true

	runs {
		configureEach {
			logLevel = org.slf4j.event.Level.DEBUG
			gameDirectory = project.file('run/' + name)
			ideName = "SC ${project.mc_version} " + ideName.get()
		}

		client {
			client()
		}

		client2 {
			client()
			gameDirectory = project.file('run/client')
			programArguments.addAll('--username', 'Dev2')
		}

		server {
			server()
			programArgument('-nogui')
		}

		data {
			clientData()
			programArguments.addAll('--mod', 'securitycraft', '--all', '--output', file('src/generated/resources/').getAbsolutePath())
		}
	}

	mods {
		securitycraft {
			sourceSet(sourceSets.main)
		}
	}
}

repositories {
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}

	exclusiveContent {
		forRepository {
			maven {
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	compileOnly "curse.maven:architectury-api-419699:5553800" //ftb teams dependency
	compileOnly "curse.maven:ftb-library-forge-404465:5557408" //ftb teams dependency
	compileOnly "curse.maven:ftb-teams-forge-404468:5448371"
	implementation "curse.maven:jei-238222:6752455"
	compileOnly "curse.maven:the-one-probe-245211:5502323"
	implementation "curse.maven:jade-324717:6674229"
	implementation "curse.maven:betterf3-401648:6685252"
	implementation "curse.maven:cloth-config-348521:6669837" //betterf3 dependency
	implementation "curse.maven:wthit-forge-455982:6678048"
	implementation "curse.maven:badpackets-615134:6671481" //wthit dependency
	compileOnly "curse.maven:projecte-226410:3955047"
	compileOnly "curse.maven:embeddium-908741:6116910"
	compileOnly "maven.modrinth:sodium:mc1.21.6-0.6.13-neoforge" //incompatible with embeddium
}

tasks.named('jar', Jar).configure {
	exclude('net/geforcemods/securitycraft/datagen/**') //exclude files from the built jar that are only used to generate the assets & data
	archiveFileName = "[${project.mc_version}] SecurityCraft v${version}.jar"
	manifest {
		attributes(["Specification-Title": "SecurityCraft",
					"Specification-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep",
					"Specification-Version": "${version}",
					"Implementation-Title": "SecurityCraft",
					"Implementation-Version": "${version}",
					"Implementation-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep"],)
	}
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xmaxerrs" << "10000"
	options.encoding = 'UTF-8'
	options.release = 21
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}