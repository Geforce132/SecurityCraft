plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.gradle.userdev' version '7.0.145'
}

import groovy.json.JsonOutput
import groovy.json.JsonSlurper

version = '1.9.10.2'
group = 'net.geforcemods.securitycraft'

base {
	archivesName = 'securitycraft'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
	duplicatesStrategy = 'include'

	//minify json resources
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
			File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources' //include generated files
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
	configureEach {
		systemProperty 'forge.logging.console.level', 'debug'
		modSource project.sourceSets.main
	}

	client {
		workingDirectory project.file('run/client')
	}

	server {
		workingDirectory project.file('run/server')
		programArgument '-nogui'
	}

	data {
		workingDirectory project.file('run/data')
		programArguments.addAll '--mod', 'securitycraft', '--all', '--output', file('src/generated/resources/').getAbsolutePath()
	}
}

repositories {
	maven {
		url "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	implementation 'net.neoforged:neoforge:20.6.119'

	implementation "curse.maven:architectury-api-419699:5383746" //ftb teams dependency
	implementation "curse.maven:ftb-library-forge-404465:5397531" //ftb teams dependency
	implementation "curse.maven:ftb-teams-forge-404468:5397629"
	implementation "curse.maven:jei-238222:5417431"
	implementation "curse.maven:the-one-probe-245211:5304322"
	implementation "curse.maven:jade-324717:5366595"
	implementation "curse.maven:betterf3-401648:5346196"
	implementation "curse.maven:cloth-config-348521:5287791" //betterf3 dependency
	implementation "curse.maven:wthit-forge-455982:5401338"
	implementation "curse.maven:badpackets-615134:5400300" //wthit dependency
//	implementation "curse.maven:projecte-226410:3955047"
	implementation "curse.maven:embeddium-908741:5352254"
}

tasks.named('jar', Jar).configure {
	exclude('net/geforcemods/securitycraft/datagen/**') //exclude files from the built jar that are only used to generate the assets & data

	manifest {
		attributes(["Specification-Title": "SecurityCraft",
					"Specification-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep",
					"Specification-Version": "${version}",
					"Implementation-Title": "SecurityCraft",
					"Implementation-Version": "${version}",
					"Implementation-Vendor": "Geforce, bl4ckscor3, Redstone_Dubstep"],)
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release = 21
}